// TODO Skipped a lot of 9.5 (networking)
// TODO Updating the eudev database after install?
// TODO Convert step3.sh to a teak script.
// TODO Tracking what files are installed.

str lfs #option;
str lfsSources #option;

void Start() {
	assert lfs != "";
	assert lfsSources != "";

	lfs = PathToAbsolute(lfs);
	lfsSources = PathToAbsolute(lfsSources);
	assert !StringContains(lfs, " ");
	SystemSetEnvironmentVariable("LFS", lfs);
	assert PathSetDefaultPrefixToScriptSourceDirectory();

	str[] directoriesToCreate = [ "/etc", "/var", "/lib64", "/tools", "/usr/bin", "/usr/lib", "/usr/sbin", "/sources" ];
	for str directory in directoriesToCreate { assert PathCreateLeadingDirectories(lfs + directory); }
	str[] userLinksToCreate = [ "/bin", "/lib", "/sbin" ];
	for str userLink in userLinksToCreate { assert SystemShellExecute("ln -s usr%userLink% %lfs%%userLink%"); }
	str[] filesToCopy = [ "step3.sh", "linux_config" ];
	for str file in filesToCopy { assert FileCopy(file, "%lfs%/%file%"); }
	for str file in DirectoryEnumerate(lfsSources):assert() { assert FileCopy("%lfsSources%/%file%", "%lfs%/sources/%file%"); }

	SystemSetEnvironmentVariable("LC_ALL", "POSIX");
	SystemSetEnvironmentVariable("LFS_TGT", StringRemoveOptionalSuffix(SystemShellEvaluate("uname -m"), "\n") + "-lfs-linux-gnu");
	SystemSetEnvironmentVariable("PATH", "%lfs%/tools/bin:/usr/bin");
	SystemSetEnvironmentVariable("CONFIG_SITE", "%lfs%/usr/share/config.site");

	str[] step2Scripts = [ "binutils.sh", "gcc.sh", "linux.sh", "glibc.sh", "libstdcpp.sh", "m4.sh", "ncurses.sh", 
		"bash.sh", "coreutils.sh", "diffutils.sh", "file.sh", "findutils.sh", "gawk.sh", "grep.sh", "gzip.sh", 
		"make.sh", "patch.sh", "sed.sh", "tar.sh", "xz.sh", "binutils2.sh", "gcc2.sh" ];
	for str script in step2Scripts { assert SystemShellExecuteWithWorkingDirectory("%lfsSources%", "/bin/bash step2/%script%"); }

	str[] directoriesToChownR = [ "/etc", "/var", "/lib64", "/tools", "/bin", "/lib", "/sbin", "/usr" ];
	for str directory in directoriesToChownR { assert SystemShellExecute("chown -R root:root $LFS%directory%"); }
	str[] directoriesToChown = [ "/dev", "/proc", "/sys", "/run" ];
	for str directory in directoriesToChown { assert SystemShellExecute("chown root:root $LFS%directory%"); }

	assert SystemShellExecute("chroot \"%lfs%\" /usr/bin/env -i HOME=/root TERM=\"$TERM\" PS1='(lfs chroot) ' PATH=/usr/bin:/usr/sbin "
		+ "/bin/bash --login /step3.sh");

	Log("-------------------------------------------------");
	Log("You are now ready to install GRUB.");
	Log("You will need to update the fstab file with the partition used.");
}
